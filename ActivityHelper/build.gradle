plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
}

android {
    namespace 'com.devmob.activityhelper'
    compileSdk 36

    defaultConfig {
        minSdk 24
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.devmob'
            artifactId = 'appmanager'
            version = '1.0.0'

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/JustZet/AppManager")

            credentials {
                username = project.findProperty("user") // gradle.properties
                password = project.findProperty("key") // gradle.properties
            }
        }
    }
}
